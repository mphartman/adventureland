##
## Test Adventure 003
##

occurs
    when !flag show_intro
    then print "Welcome!\nThis is \"test\" adventure #3.\n"
        and look
        and set_flag show_intro true

occurs
    when at front_hallway
        and here open_door
        and carrying knife
    then print "I can still hear the police outside. It doesn't look good that I'm carrying a bloody knife."

occurs
    when at outside
        and carrying knife
    then print "\nWeee-Wooo Weee-Wooo! It's the police.\nThey've spotted me carrying a weapon."

occurs
    when at outside
        and not carrying knife
    then print "Congratulations! You won the game."
        and quit


##
##  Rooms
##

room bedroom "I'm in a luxurious master bedroom with a huge king-size bed."
    exit South top_stairs

room top_stairs "I'm at the top of long flight of stairs which lead down."
    exit North bedroom
    exit Down front_hallway

room front_hallway "I'm looking north down a long hallway. There are stairs leading up."
    exit Up top_stairs

room outside "I'm outside. I've made it."
    exit South front_hallway


##
## Items
##

item knife "a bloody knife"
    inventory

item closed_door "a closed door"
    at front_hallway

item open_door "an open door"
    nowhere


##
## Actions
##

action open door
    when here closed_door
    then print "Okay, I opened it and am going through."
        and swap closed_door open_door
        and goto outside
        and look

action close door
    when here open_door
        and carrying knife
    then print "Whew! That was close. I may want to get rid of this knife."
        and swap open_door closed_door

action close door
    when here open_door
    then print "Okay, it's closed."
        and swap open_door closed_door

action "drop" knife
    when carrying knife
    then print "Good idea. Okay, I dropped it."
        and put_here knife
        and inventory
        and look




##
## Standard actions
##

wordgroup "help", "?"
wordgroup "quit"
wordgroup "inventory", "i"
wordgroup "look", "l"
wordgroup "go", "goto", "enter", "walk", "run", "exit", "leave"
wordgroup "open", "unlock"
wordgroup "get", "pickup", "grab", "take"
wordgroup "drop", "discard"
wordgroup "use"
wordgroup "north", "n"
wordgroup "south", "s"
wordgroup "up", "u"
wordgroup "down", "d"
wordgroup "east", "e"
wordgroup "west", "w"

action "go" any
    when has_exit $2
    then go $2
        and look

action (north, south, up, down, east, west)
    when has_exit
    then go
        and look

action "look"
    then look

action "inventory"
    then inventory

action "quit"
    then quit

action "help"
    then print "A voice BOOOMS out:\nTry --> \"GO, LOOK, JUMP, SWIM, CLIMB, TAKE, DROP\"\nand any other verbs you can think of..."


# Unrecognized input

action unrecognized
    then print "Sorry, I don't know how to do that."

action unrecognized unrecognized
    then print  "Sorry, I don't know how to do that with that thing."

action unrecognized any
    then print "Sorry, I don't know how to that with a {noun}."

action any unrecognized
    then print "I don't know how to {verb} with that thing."

action any none
    then print "{verb} what?"

action any any
    then print "I can't do that here right now."